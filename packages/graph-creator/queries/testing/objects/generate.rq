###############################
# Query for generating a search graph from object descriptions in dataset 'objects.ttl' from the Data Registry
###############################

PREFIX cc: <https://colonialcollections.nl/schema#> # Internal ontology
PREFIX crm: <http://www.cidoc-crm.org/cidoc-crm/>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX dig: <http://www.ics.forth.gr/isl/CRMdig/>
PREFIX gn: <http://www.geonames.org/ontology#>
PREFIX la: <https://linked.art/ns/terms/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX schema: <https://schema.org/>
PREFIX skolem: <https://triplydb.com/.well-known/genid/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

CONSTRUCT {
  ?object a cc:HeritageObject ;
    cc:additionalType ?typeName ;
    cc:about ?subjectName ;
    cc:identifier ?identifier ;
    cc:name ?name ;
    cc:description ?description ;
    cc:inscription ?inscription ;
    cc:isPartOf ?datasetName ;
    cc:material ?materialName ;
    cc:technique ?techniqueName ;
    cc:creator ?creatorName ;
    cc:owner ?ownerName ;
    cc:publisher ?publisherName ;
    cc:yearCreatedStart ?beginOfTheBeginYear ;
    cc:yearCreatedEnd ?endOfTheEndYear ;
    cc:dateCreatedStart ?beginOfTheBegin ; # For BC - remove at some point
    cc:image ?contentUrl ; # TODO: fetch from triplestore instead
    cc:locationCreated ?locationName ;
    cc:countryCreated ?countryName ;
    cc:alternateName ?alternateLocationName . # Only for indexing
}
WHERE {
  VALUES ?object {
    ?_iris
  }

  ?object a crm:E22_Human-Made_Object .

  ####################
  # Type
  ####################

  OPTIONAL {
    ?object crm:P2_has_type [
      rdfs:label ?typeName
    ] ;
  }

  ####################
  # Subject
  ####################

  OPTIONAL {
    ?object crm:P62_depicts [
      rdfs:label ?subjectName
    ] ;
  }

  ####################
  # Material
  ####################

  OPTIONAL {
    ?object crm:P45_consists_of [
      rdfs:label ?materialName
    ] ;
  }

  ####################
  # Identifier
  ####################

  OPTIONAL {
    ?object crm:P1_is_identified_by [
      a crm:E42_Identifier ;
      crm:P2_has_type <http://vocab.getty.edu/aat/300404626> ; # Identification number
      crm:P190_has_symbolic_content ?identifier
    ] ;
  }

  ####################
  # Name
  ####################

  OPTIONAL {
    ?object crm:P1_is_identified_by [
      a crm:E33_E41_Linguistic_Appellation ;
      crm:P2_has_type <http://vocab.getty.edu/aat/300404670> ; # Name
      crm:P190_has_symbolic_content ?name
    ] ;
  }

  ####################
  # Description
  ####################

  OPTIONAL {
    ?object crm:P67i_is_referred_to_by [
      crm:P2_has_type <http://vocab.getty.edu/aat/300435416> ; # Description
      crm:P190_has_symbolic_content ?description
    ] ;
  }

  ####################
  # Inscription
  ####################

  OPTIONAL {
    ?object crm:P128_carries [
      crm:P2_has_type <http://vocab.getty.edu/aat/300028702> ; # Inscription
      crm:P190_has_symbolic_content ?inscription ;
    ] ;
  }

  ####################
  # Technique
  ####################

  OPTIONAL {
    ?object crm:P108i_was_produced_by/crm:P32_used_general_technique [
      rdfs:label ?techniqueName
    ] ;
  }

  ####################
  # Part of dataset
  ####################

  ?object la:member_of ?dataset .

  OPTIONAL {
    ?dataset dct:title ?datasetName
    FILTER(LANG(?datasetName) = "" || LANGMATCHES(LANG(?datasetName), "en"))
  }

  ####################
  # Publisher of dataset
  ####################

  OPTIONAL {
    ?dataset dct:publisher [
      schema:name ?publisherName ;
    ] ;
  }

  ####################
  # Owner
  ####################

  OPTIONAL {
    ?object crm:P52_has_current_owner ?owner .
    ?owner schema:name ?ownerName .
    FILTER(LANG(?ownerName) = "" || LANGMATCHES(LANG(?ownerName), "en"))
  }

  ####################
  # Creator
  ####################

  OPTIONAL {
    ?object crm:P108i_was_produced_by/crm:P14_carried_out_by [
      rdfs:label ?creatorName
    ] ;
  }

  ####################
  # Date of creation
  ####################

  OPTIONAL {
    ?object crm:P108i_was_produced_by/crm:P4_has_time-span/crm:P82a_begin_of_the_begin ?beginOfTheBegin .
    FILTER(DATATYPE(?beginOfTheBegin) = xsd:date || DATATYPE(?beginOfTheBegin) = xsd:gYear || DATATYPE(?beginOfTheBegin) = xsd:gYearMonth)
    # Values can have an xsd date type but not be a valid date (!) - ignore these
    FILTER(COALESCE(xsd:date(STR(?beginOfTheBegin)), '!') != '!')
  }

  BIND(IF(BOUND(?beginOfTheBegin), YEAR(?beginOfTheBegin), ?null) AS ?beginOfTheBeginYear)

  OPTIONAL {
    ?object crm:P108i_was_produced_by/crm:P4_has_time-span/crm:P82b_end_of_the_end ?endOfTheEnd .
    FILTER(DATATYPE(?endOfTheEnd) = xsd:date || DATATYPE(?endOfTheEnd) = xsd:gYear || DATATYPE(?endOfTheEnd) = xsd:gYearMonth)
    # Values can have an xsd date type but not be a valid date (!) - ignore these
    FILTER(COALESCE(xsd:date(STR(?endOfTheEnd)), '!') != '!')
  }

  BIND(IF(BOUND(?endOfTheEnd), YEAR(?endOfTheEnd), ?null) AS ?endOfTheEndYear)

  ####################
  # Location of creation
  ####################

  OPTIONAL {
    ?object crm:P108i_was_produced_by/crm:P7_took_place_at ?location .
    ?location gn:name ?locationName ;
      gn:featureClass ?featureClass .
    FILTER(LANG(?locationName) = "" || LANGMATCHES(LANG(?locationName), "en"))

    OPTIONAL {
      ?location gn:alternateName ?alternateLocationName
      FILTER(LANG(?alternateLocationName) = "" || LANGMATCHES(LANG(?alternateLocationName), "en"))
    }

    # Country of which the location is a part
    OPTIONAL {
      ?location gn:parentCountry ?country .
      ?country gn:name ?tmpCountryName .
      FILTER(LANG(?tmpCountryName) = "" || LANGMATCHES(LANG(?tmpCountryName), "en"))
    }

    # Location could be a country
    BIND(IF(?featureClass = <https://www.geonames.org/ontology#A>, ?locationName, ?tmpCountryName) AS ?countryName)
  }

  ####################
  # Image
  ####################

  OPTIONAL {
    ?object crm:P65_shows_visual_item/la:digitally_shown_by [
      a dig:D1_Digital_Object ;
      crm:P2_has_type <http://vocab.getty.edu/aat/300215302> ; # Digital image
      la:access_point ?contentUrl
    ] ;
  }
}
