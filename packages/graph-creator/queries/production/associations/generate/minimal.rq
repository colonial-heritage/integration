###############################
# Query for generating a search graph from object descriptions in dataset in dataset 'Associations'
###############################

PREFIX cc: <https://colonialcollections.nl/schema#> # Internal ontology
PREFIX crm: <http://www.cidoc-crm.org/cidoc-crm/>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX la: <https://linked.art/ns/terms/>
PREFIX schema: <https://schema.org/>

CONSTRUCT {
  ?object a cc:HeritageObject ;
    cc:isPartOf ?datasetName ;
    cc:publisher ?publisherName .
}
WHERE {
  VALUES ?object {
    ?_iris
  }

  ####################
  # Part of dataset
  ####################

  ?object la:member_of ?dataset .

  OPTIONAL {
    ?dataset dct:title ?datasetName
    FILTER(LANG(?datasetName) = "" || LANGMATCHES(LANG(?datasetName), "en"))
  }

  ####################
  # Publisher of dataset
  ####################

  OPTIONAL {
    ?dataset dct:publisher [
      schema:name ?publisherName ;
    ] .
  }
}
