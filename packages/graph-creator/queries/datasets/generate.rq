###############################
# Query for generating a search graph with dataset descriptions
# Source of the query: https://triplestore.netwerkdigitaalerfgoed.nl/sparql?savedQueryName=Full%20dataset%20descriptions%20for%20publisher&owner=admin&execute
###############################

PREFIX cc: <https://colonialcollections.nl/schema#> # Internal ontology
PREFIX schema: <https://schema.org/>
PREFIX skolem: <https://triplydb.com/.well-known/genid/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>

CONSTRUCT {
  ?dataset a cc:Dataset ;
    cc:name ?name ;
    cc:publisher ?publisherName ;
    cc:license ?licenseName ;
    cc:description ?description ;
    cc:keyword ?keywords ;
    cc:dateCreated ?dateCreated ;
    cc:dateModified ?dateModified ;
    cc:datePublished ?datePublished ;
    cc:genre ?genreName ;
    cc:mainEntityOfPage ?mainEntityOfPage . # TODO: remove later
}
WHERE {
  VALUES ?dataset {
    ?_iris
  }

  ####################
  # Title
  ####################

  # Required property, but it may not exist in a specific language
  OPTIONAL {
    ?dataset schema:name ?tmpName
    FILTER(LANG(?tmpName) = "" || LANGMATCHES(LANG(?tmpName), "en"))
  }

  # TBD: add multi-language support?
  BIND(COALESCE(?tmpName, "(No name)"@en) AS ?name)

  ####################
  # License
  ####################

  ?dataset schema:license ?tmpLicense .

  # Some licenses are provided as strings (!), not IRIs - convert
  BIND(IRI(?tmpLicense) AS ?license)

  # Required property, but it may not exist in a specific language
  OPTIONAL {
    ?license a schema:DigitalDocument ;
      schema:name ?tmpLicenseName .
    FILTER(LANG(?tmpLicenseName) = "" || LANGMATCHES(LANG(?tmpLicenseName), "en"))
  }

  # TBD: add multi-language support?
  BIND(COALESCE(?tmpLicenseName, "(No name)"@en) AS ?licenseName)

  ####################
  # Publisher
  ####################

  ?dataset schema:publisher ?publisher .

  # Required property, but the publisher name may not exist in a specific language
  OPTIONAL {
    ?publisher a schema:Organization ;
      schema:name ?tmpPublisherName .
    FILTER(LANG(?tmpPublisherName) = "" || LANGMATCHES(LANG(?tmpPublisherName), "en"))
  }

  # TBD: add multi-language support?
  BIND(COALESCE(?tmpPublisherName, "(No name)"@en) AS ?publisherName)

  ####################
  # Description
  ####################

  OPTIONAL {
    ?dataset schema:description ?description
    FILTER(LANG(?description) = "" || LANGMATCHES(LANG(?description), "en"))
  }

  ####################
  # Keyword
  ####################

  OPTIONAL {
    ?dataset schema:keywords ?keywords
    FILTER(LANG(?keywords) = "" || LANGMATCHES(LANG(?keywords), "en"))
  }

  OPTIONAL { ?dataset schema:dateCreated ?dateCreated }
  OPTIONAL { ?dataset schema:dateModified ?dateModified }
  OPTIONAL { ?dataset schema:datePublished ?datePublished }

  ####################
  # Genre
  ####################

  OPTIONAL {
    ?dataset schema:additionalType ?additionalType .
    ?additionalType a skos:Concept ;
      skos:prefLabel ?genreName .
    FILTER(LANG(?genreName) = "" || LANGMATCHES(LANG(?genreName), "en"))
  }

  ####################
  # Landing page
  ####################

  OPTIONAL {
    ?dataset schema:mainEntityOfPage ?mainEntityOfPage
    # The NDE Dataset Register allows blank nodes for the landing page.
    # TriplyDB turns these into skolemized IRIs. Remove these for Elasticsearch
    FILTER(!STRSTARTS(STR(?mainEntityOfPage), STR(skolem:)))
  }
}
