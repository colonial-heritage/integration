###############################
# Query for generating a search graph with dataset descriptions
# Source of the query: https://triplestore.netwerkdigitaalerfgoed.nl/sparql?savedQueryName=Full%20dataset%20descriptions%20for%20publisher&owner=admin&execute
###############################

PREFIX cc: <https://colonialcollections.nl/schema#> # Internal ontology
PREFIX dcat: <http://www.w3.org/ns/dcat#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX schema: <https://schema.org/>
PREFIX skolem: <https://triplydb.com/.well-known/genid/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>

CONSTRUCT {
  ?dataset a cc:Dataset ;
    cc:name ?title ;
    cc:publisher ?publisherName ;
    cc:license ?licenseName ;
    cc:description ?description ;
    cc:keyword ?keyword ;
    cc:dateCreated ?created ;
    cc:dateModified ?modified ;
    cc:datePublished ?issued ;
    cc:genre ?genreName ;
    cc:mainEntityOfPage ?landingPage . # TODO: remove later
}
WHERE {
  VALUES ?dataset {
    ?_iris
  }

  ####################
  # Title
  ####################

  # Required property, but it may not exist in a specific language
  OPTIONAL {
    ?dataset dct:title ?tmpTitle
    FILTER(LANG(?tmpTitle) = "" || LANGMATCHES(LANG(?tmpTitle), "en"))
  }

  # TBD: add multi-language support?
  BIND(COALESCE(?tmpTitle, "(No name)"@en) AS ?title)

  ####################
  # License
  ####################

  ?dataset dct:license ?tmpLicense .

  # Some licenses are provided as strings (!), not IRIs - convert
  BIND(IRI(?tmpLicense) AS ?license)

  # Required property, but it may not exist in a specific language
  OPTIONAL {
    ?license a schema:DigitalDocument ;
      schema:name ?tmpLicenseName .
    FILTER(LANG(?tmpLicenseName) = "" || LANGMATCHES(LANG(?tmpLicenseName), "en"))
  }

  # TBD: add multi-language support?
  BIND(COALESCE(?tmpLicenseName, "(No name)"@en) AS ?licenseName)

  ####################
  # Publisher
  ####################

  ?dataset dct:publisher ?publisher .

  # Required property, but the publisher name may not exist in a specific language
  OPTIONAL {
    ?publisher a schema:Organization ;
      schema:name ?tmpPublisherName .
    FILTER(LANG(?tmpPublisherName) = "" || LANGMATCHES(LANG(?tmpPublisherName), "en"))
  }

  # TBD: add multi-language support?
  BIND(COALESCE(?tmpPublisherName, "(No name)"@en) AS ?publisherName)

  ####################
  # Description
  ####################

  OPTIONAL {
    ?dataset dct:description ?description
    FILTER(LANG(?description) = "" || LANGMATCHES(LANG(?description), "en"))
  }

  ####################
  # Keyword
  ####################

  OPTIONAL {
    ?dataset dcat:keyword ?keyword
    FILTER(LANG(?keyword) = "" || LANGMATCHES(LANG(?keyword), "en"))
  }

  OPTIONAL { ?dataset dct:created ?created }
  OPTIONAL { ?dataset dct:modified ?modified }
  OPTIONAL { ?dataset dct:issued ?issued }

  ####################
  # Genre
  ####################

  OPTIONAL {
    ?dataset dct:type ?genre .
    ?genre a skos:Concept ;
      skos:prefLabel ?genreName .
    FILTER(LANG(?genreName) = "" || LANGMATCHES(LANG(?genreName), "en"))
  }

  ####################
  # Landing page
  ####################

  OPTIONAL {
    ?dataset dcat:landingPage ?landingPage
    # The NDE Dataset Register allows blank nodes for the landing page.
    # TriplyDB turns these into skolemized IRIs. Remove these for Elasticsearch
    FILTER(!STRSTARTS(STR(?landingPage), STR(skolem:)))
  }
}
